plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def libArtifactId = "libcore"
def libVersionCode = 20210508
def libVersionName = "1.3.0-rc62"

android {
    compileSdkVersion 29
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode libVersionCode
        versionName libVersionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        pickFirst 'META-INF/lib_release.kotlin_module'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api 'androidx.appcompat:appcompat:1.0.2'
    api 'com.github.zj565061763:activity-event:latest.release'
    api 'com.github.zj565061763:stream:latest.release'
    api 'com.github.zj565061763:tag-view:latest.release'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "com.sd.lib.android"
                artifactId = libArtifactId
                version = libVersionName

                // 上传源码
                artifact generateSourcesJar
            }
        }
    }
}